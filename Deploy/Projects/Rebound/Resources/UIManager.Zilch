class LoadMain : ZilchEvent
{
    sends LoadMain : LoadMain;
}

class UIManager : ZilchComponent
{
	[Property]
	var HtPPanBy : Real2 = Real2(0,0);

	[Property]
	var HtPPanTime : Real = 0.0;

	[Property]
	var HtPEase : Ease = Ease.Linear;

	[Property]
	var CrPanBy : Real2 = Real2(0,0);

	[Property]
	var CrPanTime : Real = 0.0;

	[Property]
	var CrEase : Ease = Ease.Linear;

	[Property]
	var OpPanBy : Real2 = Real2(0,0);

	[Property]
	var OpPanTime : Real = 0.0;

	[Property]
	var OpEase : Ease = Ease.Linear;

	[Property]
	var HoverSoundName : String = "";
	var HoverSoundInstance : SoundInstance;

	[Property]
	var ClickSoundName : String = "";
	var ClickSoundInstance : SoundInstance;

	var ButtonList : Array[Entity] = null;

    var RefText : Entity = null;

    [Property]
    var RefTextName : String = "";
	
	var HoveredReceived : Integer;
	var DefaultReceived : Integer;

    var MainGameSpace : Space;

    function Initialize()
    {
        Daisy.Connect(this.Owner, DaisyEvent.UIButtonEvent, this.OnUIButtonEvent);
        Daisy.Connect(this.Space, DaisyEvent.LogicUpdate, this.OnLogicUpdate);
        Daisy.Connect(this.Space, DaisyEvent.ReportSpace, this.OnReportSpace);
        Daisy.Connect(this.Space, DaisyEvent.LoadMain, this.OnLoadMain);
		
		this.HoveredReceived = 0;
		this.DefaultReceived = 0;

        this.ButtonList = new Array[Entity]();
        
        var childList = this.Owner.Children.All;
        
        while (childList.IsNotEmpty)
        {
            this.ButtonList.Add(childList.Current);
            
            Console.WriteLine("Adding child");
            
            childList.MoveNext();
        }

        if(this.RefTextName != "")
        {
            this.RefText = this.Space.FindObjectByName(this.RefTextName);
        }
    }

    function OnReportSpace(event : ReportSpace)
    {
        this.MainGameSpace = event.MainSpace;
    }

    function OnUIButtonEvent(event : UIButtonEvent)
    {
    	for (var i = 0; i < this.ButtonList.Count; i += 1)
        {
            if(event.Button == this.ButtonList[i])
            {
                if(event.State == ButtonState.Default)
                {
                    this.OnButtonDefault(this.ButtonList[i]);
                }
                
                if(event.State == ButtonState.Hovered)
                {
                    this.OnButtonHovered(this.ButtonList[i]);
                }
                
                if(event.State == ButtonState.Clicked)
                {
                    this.OnButtonClicked(this.ButtonList[i]);
                }
            }
        }
    }

    function OnLogicUpdate(event : LogicUpdate)
    {
        if(this.RefText == null || this.RefText.SpriteText == null)
        {
            return;
        }
		
		if(this.DefaultReceived - this.HoveredReceived > 4)
		{
			this.RefText.SpriteText.Text = "";
		}
    }

    function OnButtonDefault(button : Entity)
    {
		this.DefaultReceived += 1;
    }

    function OnButtonHovered(button : Entity)
    {
    	if(this.HoverSoundName != "")
    	{
    		this.HoverSoundInstance = this.Space.SoundSpace.PlayCue(this.HoverSoundName);
    	}

        if(this.RefText == null || this.RefText.SpriteText == null)
        {
            return;
        }

    	if(button.Name == "NewGameButton")
        {
            this.RefText.SpriteText.Text = "     Play Rebound";
        }
        else if(button.Name == "ResumeGameButton")
        {
            this.RefText.SpriteText.Text = "   Return to Rebound";
        }
        else if(button.Name == "HowToPlayButton")
        {
            this.RefText.SpriteText.Text = "  View the game's              controls";
        }
        else if(button.Name == "OptionsButton")
        {
            this.RefText.SpriteText.Text = "Change the game's             settings";
        }
        else if(button.Name == "CreditsButton")
        {
            this.RefText.SpriteText.Text = "Learn about the team";
        }
        else if(button.Name == "ReturnButton")
        {
            this.RefText.SpriteText.Text = "Return to the Main Menu";
        }
        else if(button.Name == "QuitGameButton")
        {
            this.RefText.SpriteText.Text = "    Leave the game";
        }
		this.HoveredReceived += 1;
    }

    function OnButtonClicked(button : Entity)
    {
    	if(this.ClickSoundName != "")
    	{
    		this.ClickSoundInstance = this.Space.SoundSpace.PlayCue(this.ClickSoundName);
    	}

    	if(button.Name == "NewGameButton")
    	{
    		this.Space.Dispatch(DaisyEvent.StartGame, StartGame());
    	}
        else if(button.Name == "ResumeGameButton" && this.MainGameSpace != null)
        {
            this.MainGameSpace.Dispatch(DaisyEvent.PauseEvent, PauseEvent());
        }
    	else if(button.Name == "HowToPlayButton")
    	{
            var htpPan = PanEvent();
            htpPan.PanBy = this.HtPPanBy;
            htpPan.PanTime = this.HtPPanTime;
            htpPan.PanEase = this.HtPEase;

            this.Space.Dispatch(DaisyEvent.PanEvent, htpPan);
    	}
    	else if(button.Name == "OptionsButton")
    	{
    		var opPan = PanEvent();
            opPan.PanBy = this.OpPanBy;
            opPan.PanTime = this.OpPanTime;
            opPan.PanEase = this.OpEase;

            this.Space.Dispatch(DaisyEvent.PanEvent, opPan);
    	}
    	else if(button.Name == "CreditsButton")
    	{
    		var crPan = PanEvent();
            crPan.PanBy = this.CrPanBy;
            crPan.PanTime = this.CrPanTime;
            crPan.PanEase = this.CrEase;

            this.Space.Dispatch(DaisyEvent.PanEvent, crPan);
    	}
        else if(button.Name == "ReturnButton")
        {
            var cSpace = this.GameSession.CreateSpace("ConfirmationSpace", true);
            cSpace.LoadLevel("ConfirmReturn");

            var disableEvent = EnableOrDisableButton();
            disableEvent.Enable = false;
            
            this.Space.Dispatch(DaisyEvent.EnableOrDisableButton, disableEvent);

            var reportSpace = ReportSpace();
            reportSpace.MainSpace = this.Space;

            cSpace.Dispatch(DaisyEvent.ReportSpace, reportSpace);
        }

    	else if(button.Name == "QuitGameButton")
    	{
    		var cSpace = this.GameSession.CreateSpace("ConfirmationSpace", true);
            cSpace.LoadLevel("ConfirmQuit");

            var disableEvent = EnableOrDisableButton();
            disableEvent.Enable = false;
            
            this.Space.Dispatch(DaisyEvent.EnableOrDisableButton, disableEvent);

            var reportSpace = ReportSpace();
            reportSpace.MainSpace = this.Space;

            cSpace.Dispatch(DaisyEvent.ReportSpace, reportSpace);
    	}
        else if(button.Name == "ConfirmQuitButton")
        {
            this.GameSession.Quit();
        }
        else if(button.Name == "ConfirmReturnButton")
        {
            this.Space.Destroy();
            this.MainGameSpace.Dispatch(DaisyEvent.EnableOrDisableButton, EnableOrDisableButton());
            this.MainGameSpace.Dispatch(DaisyEvent.LoadMain, LoadMain());
        }
        else if(button.Name == "RetractQuitButton")
        {
            this.MainGameSpace.Dispatch(DaisyEvent.EnableOrDisableButton, EnableOrDisableButton());

            this.Space.Destroy();
        }
    }

    function OnLoadMain(event : LoadMain)
    {
        this.MainGameSpace.Dispatch(DaisyEvent.PauseEvent, PauseEvent());
        this.MainGameSpace.LoadLevel("MainMenu");
    }
}
