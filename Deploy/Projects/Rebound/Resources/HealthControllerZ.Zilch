class DeathEvent : ZilchEvent {}

class HealthControllerZ : ZilchComponent
{
    [Property] var MaxHealth : Real = 0;
    [Property] var  StartingHealth : Real = 0;
    [Property] var  IsInvulnerable : Boolean = false;
    [Property] var  InvTime : Real = 0;
    [Property] var DamagedColor : Real4 = Real4(0,0,0,0);
    var CurrentTime : Real = 0;

    var  Health : Real = 0;

    function Initialize()
    {
        Daisy.Connect(this.Space, DaisyEvent.LogicUpdate, this.OnLogicUpdate);
        this.Health = this.StartingHealth;
    }

    function OnLogicUpdate(event : LogicUpdate)
    {
      if(this.CurrentTime  > 0)
      {
        this.CurrentTime -= event.Dt;
      }
      
    }

    function ModifyHealth(amount : Real) : Boolean
    {
      if(this.CurrentTime  > 0)
      {
        return false;
      }
      
      this.CurrentTime = this.InvTime;
      var oldHealth = this.Health;

      if (!this.IsInvulnerable)
      {
        this.Health += amount;
        if(amount < 0)
        {
        	this.Feedback();
        }
        

        if (this.Health > this.MaxHealth)
        {
          this.Health = this.MaxHealth;
        }
        if (this.Health < 0)
        {
          this.Health = 0;
        }
      }

      if (this.Health <= 0)
      {
        var deathEvent = DeathEvent();
        this.Owner.Dispatch(DaisyEvent.DeathEvent, deathEvent);
      }

      if (oldHealth == this.Health)
        {
        return false;
        }
      else
        {
          return true;
        }
    }

    function Feedback()
    {
    	var damSeq = Actions.Sequence(this.Owner.Actions);
    	Actions.Property(damSeq, @this.Owner.Sprite.Color, this.DamagedColor, 0.06, Ease.Linear);
    	Actions.Property(damSeq, @this.Owner.Sprite.Color, Real4(1,1,1,1), 0.06, Ease.Linear);
    	Actions.Property(damSeq, @this.Owner.Sprite.Color, this.DamagedColor, 0.06, Ease.Linear);
    	Actions.Property(damSeq, @this.Owner.Sprite.Color, Real4(1,1,1,1), 0.06, Ease.Linear);
    }

    function GetHealth() : Real
    {
      return this.Health;
    }

}
